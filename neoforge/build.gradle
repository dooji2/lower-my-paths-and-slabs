plugins {
    id 'multiloader-loader'
    id 'net.neoforged.moddev'
}

repositories {
	exclusiveContent {
		forRepository {
			maven {
				name = "Modrinth"
				url = "https://api.modrinth.com/maven"
			}
		}
		filter {
			includeGroup "maven.modrinth"
		}
	}
}

neoForge {
    version = neoforge_version
    // Automatically enable neoforge AccessTransformers if the file exists
    def at = project(':common').file('src/main/resources/META-INF/accesstransformer.cfg')
    if (at.exists()) {
        accessTransformers.from(at.absolutePath)
    }
    parchment {
        minecraftVersion = parchment_minecraft
        mappingsVersion = parchment_version
    }
    runs {
        configureEach {
            systemProperty('neoforge.enabledGameTestNamespaces', mod_id)
            ideName = "NeoForge ${it.name.capitalize()} (${project.path})" // Unify the run config names with fabric
        }
        client {
            client()
        }
        data {
            data()

            // DataGen can be run by - "./gradlew :neoforge:runData" in Terminal.
            // Specify the modid for data generation, where to output the resulting resource, and where to look for existing resources.
            programArguments.addAll '--mod', project.mod_id, '--all', '--output', file('src/generated/resources/').getAbsolutePath(), '--existing', file('src/main/resources/').getAbsolutePath()

        }
        server {
            server()
        }
    }
    mods {
        "${mod_id}" {
            sourceSet sourceSets.main
        }
    }
}

dependencies {
    compileOnly "maven.modrinth:AANobbMI:${project.neoforge_sodium_version}"
    if (!project.hasProperty("noSodiumRuntime")) {
        runtimeOnly "maven.modrinth:AANobbMI:${project.neoforge_sodium_version}"
    }
    compileOnly "maven.modrinth:YL57xq9U:${project.neoforge_iris_version}"
    if (!project.hasProperty("noIrisRuntime")) {
        runtimeOnly "maven.modrinth:YL57xq9U:${project.neoforge_iris_version}"
    }
}

sourceSets.main.resources { srcDir 'src/generated/resources' }

tasks.named('jar') {
    def minecraftVersion = project.minecraft_version
    def modVersion = project.version
    def namespace = project.mod_id

    archiveBaseName.set("${namespace}-${modVersion}-neoforge-mc${minecraftVersion}")
    archiveVersion.set("")
}