plugins {
    id 'multiloader-loader'
    id 'fabric-loom'
}

repositories {
	exclusiveContent {
		forRepository {
			maven {
				name = "Modrinth"
				url = "https://api.modrinth.com/maven"
			}
		}
		filter {
			includeGroup "maven.modrinth"
		}
	}
}

dependencies {
    minecraft "com.mojang:minecraft:${minecraft_version}"
    mappings loom.layered {
        officialMojangMappings()
        parchment("org.parchmentmc.data:parchment-${parchment_minecraft}:${parchment_version}@zip")
    }
    modImplementation "net.fabricmc:fabric-loader:${fabric_loader_version}"
    modImplementation "net.fabricmc.fabric-api:fabric-api:${fabric_version}"

	modCompileOnly "maven.modrinth:AANobbMI:${project.fabric_sodium_version}"
    if (!project.hasProperty("noSodiumRuntime")) {
        modLocalRuntime "maven.modrinth:AANobbMI:${project.fabric_sodium_version}"
    }
    modCompileOnly "maven.modrinth:YL57xq9U:${project.fabric_iris_version}"
    if (!project.hasProperty("noIrisRuntime")) {
        modLocalRuntime "maven.modrinth:YL57xq9U:${project.fabric_iris_version}"
    }
}

loom {
    def aw = project(':common').file("src/main/resources/${mod_id}.accesswidener")
    if (aw.exists()) {
        accessWidenerPath.set(aw)
    }
    mixin {
        defaultRefmapName.set("${mod_id}.refmap.json")
    }
    runs {
        client {
            client()
            setConfigName('Fabric Client')
            ideConfigGenerated(true)
            runDir('runs/client')
        }
        server {
            server()
            setConfigName('Fabric Server')
            ideConfigGenerated(true)
            runDir('runs/server')
        }
    }
}

tasks.named('remapJar') {
    def minecraftVersion = project.minecraft_version
    def modVersion = project.version
    def namespace = project.mod_id

    archiveBaseName.set("${namespace}-${modVersion}-fabric-mc${minecraftVersion}")
    archiveVersion.set("")
}